# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.

from snakemake.utils import min_version, validate
from snakemake.remote import FTP, HTTP

FTP = FTP.RemoteProvider()
HTTP = HTTP.RemoteProvider()

import pandas as pd

##### set minimum snakemake version #####
min_version("7.22.0")


configfile: "config/config.yaml"


validate(config, "schemas/config.schema.yaml")


wildcard_constraints:
    reptype="\w+",
    sample="\w+",
    individual="\w+",
    lane="\d+",


samples = pd.read_csv(config["samples"], sep="\t", dtype=str).set_index(
    ["individual_id", "platform", "sample_id", "lane_id"], drop=False
)
samples.sort_index(inplace=True)
validate(samples, "schemas/samples.schema.yaml")


def is_paired_end(individual, sample, lane):
    ss = samples.loc[(individual, "illumina", sample, lane), :]
    # check that r2 is present and not NaN for this sample
    if ("r2" in ss.index) and (ss.r2 == ss.r2):
        return True
    else:
        return False


samples["units"] = samples.apply(
    lambda row: ["r1", "r2"]
    if is_paired_end(row.individual_id, row.sample_id, row.lane_id)
    else "r1",
    axis=1,
)


include: "rules/trim.smk"
include: "rules/qc.smk"
include: "rules/ref.smk"
include: "rules/longranger.smk"
include: "rules/bwa.smk"
include: "rules/xtea.smk"


# determine if each individual has multiple platforms
platforms = {}
for i in set(samples["individual_id"]):
    p = set(samples.loc[i, "platform"])
    if "illumina" in p and "10x" in p:
        platforms[i] = "illumina_10x"
    else:
        platforms[i] = samples.loc[i, "platform"].iloc[0]


rule all:
    input:
        expand(
            rules.multiqc.output,
            outdir=config["outdir"],
        ),
        expand(
            expand(
                rules.run_xtea.output,
                reptype=config["reptype"],
                outdir=config["outdir"],
                allow_missing=True,
            ),
            zip,
            individual=platforms.keys(),
            platform=platforms.values(),
            allow_missing=True,
        ),
